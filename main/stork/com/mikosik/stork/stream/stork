
  build {
    module
      (import("stork.optional.present"))
      (import("stork.optional.absent"))
      (import("stork.integer.add"))
      (import("stork.integer.moreThan"))
      (import("stork.integer.increment"))
      (import("stork.integer.decrement"))
      (import("stork.function.flip"))
      (import("stork.boolean.false"))
      (import("stork.boolean.or"))
  }

  some(head)(tail)(vSome)(vNone) {
    vSome(head)(tail)
  }

  none(vSome)(vNone) {
    vNone
  }



  reduce(sum)(add)(stream) {
    stream
      ((head)(tail) {
        reduce
          (add(head)(sum))
          (add)
          (tail)
      })
      (sum)
  }

  tryAt(index)(stream) {
    tryFirst(skip(index)(stream))
  }

  length(stream) {
    stream
      ((head)(tail){ add(1)(length(tail)) })
      (0)
  }

  single(element) {
    some(element)(none)
  }

  repeat(element) {
    some
      (element)
      (repeat(element))
  }

  tryFirst(stream) {
    stream
      ((head)(tail){ present(head) })
      (absent)
  }

  limit(n)(stream) {
    moreThan(0)(n)
      (stream
        ((head)(tail) {
          some(head)(limit(decrement(n))(tail))
        })
        (none)
      )
      (none)
  }

  skip(n)(stream) {
    moreThan(0)(n)
      (stream
        ((head)(tail) {
          skip(decrement(n))(tail)
        })
        (none)
      )
      (stream)
  }

  filter(condition)(stream) {
    stream
      ((head)(tail) {
        condition(head)
          (some(head)(filter(condition)(tail)))
          (filter(condition)(tail))
      })
      (none)
  }

  while(condition)(stream) {
    stream
      ((head)(tail) {
        condition(head)
          (some(head)(while(condition)(tail)))
          (none)
      })
      (none)
  }

  contains(condition)(stream){
    stream
      ((head)(tail) {
        or
          (condition(head))
          (contains(condition)(tail))
      })
      (false)
  }

  tryFind(condition)(stream) {
    tryFirst(filter(condition)(stream))
  }

  each(transform)(stream) {
    stream
      ((head)(tail) {
        some
          (transform(head))
          (each(transform)(tail))
      })
      (none)
  }

  reverse {
    reduce(none)(some)
  }

  append(end)(stream) {
    stream
      ((head)(tail) {
        some(head)(append(end)(tail))
      })
      (end)
  }

  prepend {
    flip(append)
  }

  flatten {
    reduce(none)(append)
  }

  array(size) {
    array2(size)(none)
  }

  array2(size)(collected) {
    moreThan(0)(size)
      (array3(size)(collected))
      (reverse(collected))
  }

  array3(size)(collected)(element) {
    array2
      (decrement(size))
      (some(element)(collected))
  }

  generate(next)(current) {
    some
      (current)
      (generate(next)(next(current)))
  }

  for(initial)(condition)(next) {
    while(condition)(generate(next)(initial))
  }

  count {
    countFrom(0)
  }

  countFrom {
    generate(increment)
  }
